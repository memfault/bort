// Top-level build file where you can add configuration options common to all sub-projects/modules.

import static com.memfault.bort.buildsrc.BortSettingsDownloaderKt.fetchBortSettings

buildscript {
    apply from: 'properties.gradle'

    gradle.taskGraph.whenReady { graph ->
        verifyBortProperties()
        warnLegacyBortProperties()
    }

    ext.versions = [
            'minSdk'                           : bortProperty("MIN_SDK_VERSION") as Integer,
            'targetSdk'                        : bortProperty("TARGET_SDK_VERSION") as Integer,
            'compileSdk'                       : 33
    ]

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }

    }
}

plugins {
    alias(deps.plugins.android.application) apply false
    alias(deps.plugins.android.library) apply false
    alias(deps.plugins.dagger.anvil) apply false
    alias(deps.plugins.dagger.hilt) apply false
    alias(deps.plugins.kotlin) apply false
    alias(deps.plugins.kotlin.ktlint) apply false
    alias(deps.plugins.kotlin.serialization) apply false
    alias(deps.plugins.gradle.versions) apply true
}

// Configure gradle-versions plugin to only tell us about stable releases.
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}
tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

// Try to refresh settings/settings.json before configuring the :bort project:
gradle.beforeProject { project ->
    if (project.path != ":bort") return

    fetchBortSettings(
        rootProject.rootDir,
        rootProject.properties["MEMFAULT_API_BASE_URL"],
        bortProperty("MEMFAULT_PROJECT_API_KEY"),
        rootProject.properties["BORT_USE_DEVELOPMENT_SETTINGS_JSON"] == "1",
        rootProject.properties["SKIP_DOWNLOAD_SYSTEM_SETTINGS_JSON"] == "1",
        { name -> bortProperty(name) },
        { msg, exc -> rootProject.logger.log(LogLevel.WARN, msg,
            exc == null ? "" : exc) }
    )
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        version = "0.48.2"
        android = true
        outputColorName = "RED"
    }
}
